buildscript {
    rootProject.apply {
        from(rootProject.file("versions.gradle"))
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

dependencies {
    compile project(":p8e-proto-internal")

    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    // Encryption
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: "$bouncy_castle"
    implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: "$bouncy_castle"

    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"

    // GRPC
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpc_version
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpc_version
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/kotlin'
        }
    }
    test {
        java {
            srcDirs += 'src/test/kotlin'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        // Optional: an artifact spec for a protoc plugin, with "grpc" as
        // the identifier, which can be referred to in the "plugins"
        // container of the "generateProtoTasks" closure.
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc {}
        }
    }
}
