buildscript {
    rootProject.apply {
        from(rootProject.file("versions.gradle"))
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
    }
}

apply plugin: 'com.google.protobuf'
sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/java'
sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/grpc'

dependencies {
    compile project(":p8e-proto-internal")

    compile "com.google.protobuf:protobuf-java:$protobuf_version"
    compile "com.google.protobuf:protobuf-java-util:$protobuf_version"

    // GRPC
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpc_version
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpc_version

    // Annotations
    implementation("javax.annotation:javax.annotation-api:$javax_annotation_version")
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        // Optional: an artifact spec for a protoc plugin, with "grpc" as
        // the identifier, which can be referred to in the "plugins"
        // container of the "generateProtoTasks" closure.
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc {}
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
